---
title: ✨100nm PMOS
abstract: 使用 athena 来设置 pmos
link: https://scholarworks.rit.edu/cgi/viewcontent.cgi?article=9359&context=theses
---

PPT：<https://gnusha.org/~nmz787/mems/unorganized/AdvCmosProcessSimulation.pdf>

## Structure1

```python
# 100nm 有效沟道长度 PMOS 建模
# structure1: N-well

go athena

# 定义网格
line x location=0.00 spacing=0.1
line x location=0.8 spacing=0.01
line x location=1.0 spacing=0.01

line y location=0.00 spacing=0.001
line y location=0.1 spacing=0.01
line y location=0.2 spacing=0.1
line y location=0.5 spacing=0.1

# 扩散模型为 Fully coupled model
method full.cpl


# p-type 晶圆
init silicon boron resistivity=20 orientation=100 two.d

# nwell
deposit oxide thick=0.043
implant phosphor dose=2.0e13 energy=170 monte rotation=45 crystal
#tonyplot

# well drive in
diffus time=20 temp=800 t.final=1000 nitro
diffus time=240 temp=1000 nitro
diffus time=20 temp=1000 t.final=800 nitro


# extract the junction depth of pwell
extract name="junction_depth_of_pwell" xj material="Silicon" mat.occno=1 x.val=0.35 \
 junc.occno=1
#
implant phosphor dose=7.0e13 energy=68 monte rotation=45 crystal 
extract name="boron_vt_adjust" area from curve(depth,impurity="Net Doping" \
 material="Silicon" mat.occno=1 y.val=0.2)/10000 outfile="vtadjust.dat"

struct outfile=rev1.str
quit
```

## Structure2

```python
# 100nm 有效沟道长度 PMOS 建模
# structure2

go athena
method full.cpl
# 加载之前的结构文件
init infile=rev1.str
# etch pad oxide
etch oxide dry thick=.0485
#tonyplot

# gate oxide growth
deposit oxynitride thick=0.003 divisions=5
diffus time=10 temp=800 t.final=900 nitro
diffus time=20 temp=900 nitro
diffus time=10 temp=900 t.final=800 nitro

# deposit polysilicon 
deposit polysilicon thick=0.25 dy=0.02
#tonyplot
# 
etch polysilicon start x=0 y=0.4
etch cont x=0 y=-0.6
etch cont x=0.9375 y=-0.6
etch done x=0.9375 y=0.4
#tonyplot
#
etch oxynitride left p1.x=0.9375
#
deposit oxide thick=0.025
#tonyplot
diffus time=20 temp=800 t.final=1000 nitro
diffus time=15 temp=1000 nitro
diffus time=20 temp=1000 t.final=800 nitro
#tonyplot
struct outfile=100nmPMOS2.str
quit
```

## Structure3

```python
# 100nm 有效沟道长度 PMOS 建模
# structure3

go athena

method full.cpl

# load the previous structure file
init infile=100nmPMOS2.str

# ldd implant
implant bf2 dose=9.0e14 energy=20 monte n.ion=100000 smooth=0.25 \
 rotation=45

# nitride spacers
deposit nitride thick=0.25

# etch nitride to form sidewall spacers
etch nitride dry thick=0.25
#tonyplot

# source drain implants 
implant bf2 dose=5.0e15 energy=28 monte n.ion=100000 smooth=0.25 \
 rotation=45 

#anneal 
diffus time=3/60 temp=700 t.final=1050 nitro
# 
diffus time=5/60 temp=1050 
#
diffus time=6/60 temp=1050 t.final=700 nitro
struct mirror right
#tonyplot
#quit

# etch oxide
etch oxide dry thick=0.025
struct outfile=100nmPMOS3.str
#tonyplot 
quit

```

## Structure4

```python
# 100nm 有效沟道长度 PMOS 建模
# structure4

go athena
init infile=100nmPMOS3.str

# 
deposit titanium thick=0.03 divisions=10 dy=0.015 
#
diffuse time=5/60 temp=700 nitro
# 
etch titanium all 
#
diffuse time=5/60 temp=700 nitro


# 
deposit oxide thick=0.30
# struct mirror right
#tonyplot


# 
etch oxide start x=.20 y=1.00
etch cont x=.20 y=-1.00
etch cont x=.40 y=-1.00
etch done x=.40 y=1.00
# 
etch oxide start x=.95 y=1.00
etch cont x=.95 y=-1.00
etch cont x=1.05 y=-1.00
etch done x=1.05 y=1.00 
#
etch oxide start x=1.6 y=1.00
etch cont x=1.6 y=-1.00
etch cont x=1.8 y=-1.00
etch done x=1.8 y=1.00 

etch oxide above p1.y=-0.28
struct outfile=100nmPMOS4.str
#tonyplot
quit
```

## Structure5

```python
# 100nm 有效沟道长度 PMOS 建模
# structure5

go athena
init infile=100nmPMOS4.str
# 
deposit aluminum thick=0.4 divisions=10
# 
#tonyplot
# 
etch aluminum above p1.y=-0.265
#tonyplot
electrode name=gate x=1.0 y=0.1
electrode name=source x=0.3
electrode name=drain x=1.7
electrode name=substrate backside
#tonyplot
struct outfile=pmosvt.str
quit
```

## 转移特性

```python
# 100nm 有效沟道长度 PMOS 建模
# pmos_iv: 转移特性

go atlas simflags="-P 1"
init infile=pmosvt.str
#tonyplot 100nmPMOS3.str

#set material models
models cvt srh print 
contact name=gate p.poly
interface qf=3e11

# get initial solution
solve init
method newton trap
solve prev

# Bias the drain a bit...
solve vdrain=-0.1 name=drain

# Ramp the gate
log outf=mos1ex08_1.log master
solve vgate=0 vstep=-0.01 vfinal=-1.2 vdrain=-0.1 name=gate
save outf=mos1ex08_1.str

# extract device parameters......
extract init inf="mos1ex08_1.log"
extract name="pvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) - abs(ave(v."drain"))/2.0)
#pvt=0.585V

# plot results
tonyplot mos1ex08_1.log -set mos1ex08_1_log.set
```

## 输出特性

```python
# 100nm 有效沟道长度 PMOS 建模
# pmos_output: 输出特性

go atlas2 simflags="-P 1"

init infile=pmosvt.str

# define the Gate workfunction 
contact name=gate p.poly

# Define the Gate Qss
interface qf=3e11

# Use the cvt mobility model for MOS 
models cvt srh 

# set gate biases with Vds=0.0 
solve init
solve vgate=-0.3 outf=solve_tmp1 
solve vgate=-0.6 outf=solve_tmp2 
solve vgate=-0.9 outf=solve_tmp3 
solve vgate=-1.2 outf=solve_tmp4

#load in temporary files and ramp Vds
load infile=solve_tmp1
log outf=mos1ex09_1.log
solve name=drain vdrain=0 vfinal=-1.2 vstep=-0.1 
load infile=solve_tmp2
log outf=mos1ex09_2.log
solve name=drain vdrain=0 vfinal=-1.2 vstep=-0.1
 
load infile=solve_tmp3
log outf=mos1ex09_3.log
solve name=drain vdrain=0 vfinal=-1.2 vstep=-0.1
load infile=solve_tmp4
log outf=mos1ex09_4.log
solve name=drain vdrain=0 vfinal=-1.2 vstep=-0.1

# extract max current and saturation slope 
extract name="pidsmax" max(abs(i."drain"))
extract name="p_sat_slope" slope(minslope(curve(abs(v."drain"),abs(i."drain"))))
tonyplot -overlay mos1ex09_1.log mos1ex09_2.log mos1ex09_3.log mos1ex09_4.log -set mos1ex09_1.set

quit
```

## DIBL

```python
# 100nm 有效沟道长度 PMOS 建模
# DIBL

go atlas simflags="-P 1”
init infile=pmosvt.str
# set material models
models cvt srh print 
contact name=gate p.poly
interface qf=3e11

# get initial solution
solve init
method newton trap maxtraps=8 autonr itlimit=30
solve prev

# Bias the drain a bit...
solve vdrain=-0.025 vstep=-0.025 vfinal=-0.1 name=drain

# Ramp the gate
log outf=mos1ex11_1.log master
solve vgate=0 vstep=-0.01 vdrain=-0.1 vfinal=-1.2 \
name=gate cname=drain

# extract device parameters
extract init inf="mos1ex11_1.log"
extract name="pvt1" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
 - abs(ave(v."drain"))/2.0)
# now open a dummy log file...
log off


# Now start again and ramp the drain to -1.2 volts...
solve init
# Bias the drain to 3 volts......slowly at first....
solve vdrain=-0.025 vstep=-0.025 vfinal=-0.1 name=drain
solve vdrain=-0.25 vstep=-0.025 vfinal=-1.2 name=drain

# Ramp the gate again with another opened logfile...
log outf=mos1ex11_2.log master
solve vgate=0 vstep=-0.01 vdrain=-1.2 vfinal=-1.2 name=gate \
cname=drain

# extract the next device parameter wuth the drain now at -1.2 volts....
extract init inf="mos1ex11_2.log"
#extract name="pvt2" x.val from curve(abs(v."gate"),abs(i."drain")) where y.val=0.1e-6
extract name="pvt2" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
 - abs(ave(v."drain"))/2.0)

# Calculate a DIBL parameter....in V/V
extract name="pdibl" ($"pvt1"-$"pvt2")/(1.2-0.1)
# 0.585-0.227
tonyplot -overlay mos1ex11_1.log mos1ex11_2.log -set mos1ex11_log.set
quit
```


## 栅、衬底电流（fail）

```python
# 100nm 有效沟道长度 PMOS 建模
# pmos_ig_isub: 输出特性


go devedit

init infile=pmosvt.str

# Set Meshing Parameters
#
base.mesh height=0.1 width=0.1
#
bound.cond !apply max.slope=28 max.ratio=300 rnd.unit=0.001 line.straightening=1 align.points when=automatic
#
imp.refine imp="NetDoping" sensitivity=1
imp.refine min.spacing=0.02
#
constr.mesh max.angle=90 max.ratio=300 max.height=1 \
	max.width=1 min.height=0.0001 min.width=0.0001
#
# Perform mesh operations
#
Mesh Mode=MeshBuild
structure outf=mos1ex06_0x.str

refine mode=y x1=0.9 y1=0 x2=1.1 y2=0.02
refine mode=y x1=0.9 y1=0 x2=1.1 y2=0.01
refine mode=both x1=1.1 y1=0 x2=1.3 y2=0.05
refine mode=y x1=0 y1=0.1 x2=2.0 y2=0.37
refine mode=y x1=0 y1=0.1 x2=2.0 y2=0.33
refine mode=y x1=1.4 y1=0.05 x2=2.0 y2=0.1

structure outf=mos1ex06_0.str
tonyplot  mos1ex06_0.str 
#-set mos1ex06_0.set

go atlas

models  cvt srh print hcte.el hei nearflg

impact  selb  length.rel   lrel.el=0.02
contact name=gate n.poly
interface qf=3e10

material taun0=1e-7 taup0=1e-7
material  taurel.el=0.2e-12 taumob.el=0.2e-12


solve init

method newton trap temp.tol=1.e-4

solve vdrain=-0.01 vstep=-0.01 vfinal=-0.2 name=drain

log outf=mos1ex06.log master

method newton trap temp.tol=1.e-4
solve vgate=-0.025 vstep=-0.025 vfinal=-0.4  name=gate 
#solve vgate=-0.5 vstep=-0.1 vfinal=-1.2 name=gate 


tonyplot  mos1ex06.log #-set mos1ex06_log.set

# Extract Substrate current peak value parameter....
extract init inf="mos1ex06.log"
extract name="pmax_isub_vd1.2" max(curve(abs(v."gate"),abs(i."substrate")))

# Extract gate voltage at the peak substrate current point....
extract init inf="mos1ex06.log"
extract name="pVgate_at_isubmax_vd1.2" x.val from curve(abs(v."gate"),abs(i."substrate")) where y.val=$"pmax_isub_vd1.2"

# Extract Gate current peak value parameter....
extract init inf="mos1ex06.log"
extract name="pmax_igate_vd1.2" (max(curve(abs(v."gate"),abs(i."gate"))))


# Extract gate voltage at the peak gate current point....
extract init inf="mos1ex06.log"
extract name="pVgate_at_isubmax_vd1.2" x.val from curve(abs(v."gate"),abs(i."gate")) where y.val=$"pmax_igate_vd1.2"


quit
```

## 热电子注入

```python
# 100nm 有效沟道长度 PMOS 建模
# pmos_hei: 输出特性

### PRE-STRESSED TEST
go atlas
# IMPORT THE MESH
mesh inf=pmosvt.str master.in
# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
solve init 
save outf=mos2ex02_1.str 
tonyplot mos2ex02_1.str -set mos2ex02_1.set

method newton trap carr=1 electron
solve prev
solve vdrain=-0.1
log outf=mos2ex02_2.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

### DEVICE STRESSING
go atlas
# IMPORT DEVICE STRUCURE
mesh inf=pmosvt.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh cvt hei hcte.el devdeg.e nearflg ig.elinf=9.4e-7
#impact  selb
degradation f.nta=mos2ex02_devdeg.nta sigmae=2.e-15 sigmah=1.e-17

 
# SOLUTIONS
solve init
method newton trap carr=1
solve
solve vgate=-1.2
solve vdrain=-0.1 vstep=-0.05 name=drain vfinal=-1.2
solve vdrain=-1.2

method  newton trap quasistatic climit=1e-5 carr=2 dt.max=1000
log outf=mos2ex02_3.log
output devdeg
solve tstep=1e-5 tstop=0.01 
save outf=mos2ex02_0_01s.str
solve tstep=1e-3 tstop=0.1 
save outf=mos2ex02_0_1s.str
solve tstep=1e-2 tstop=1 
save outf=mos2ex02_1s.str
solve tstep=1e-2 tstop=1000 
save outf=mos2ex02_1000s.str
solve tstep=1e3 tstop=10000 
save outf=mos2ex02_10000s.str

### STRESSED TEST AT 0.01 s
go atlas
# IMPORT MESH
mesh inf=mos2ex02_0_01s.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
load infile=mos2ex02_0_01s.str master
solve init 
method newton trap carr=1 electron
solve
solve vdrain=-0.1

log outf=mos2ex02_0_01s.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

### STRESSED TEST AT 0.1 s
go atlas
# IMPORT MESH
mesh inf=mos2ex02_0_1s.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
load infile=mos2ex02_0_1s.str master
solve init 
method newton trap carr=1 electron
solve
solve vdrain=-0.1

log outf=mos2ex02_0_1s.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

### STRESSED TEST AT 1 s
go atlas
# IMPORT MESH
mesh inf=mos2ex02_1s.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
load infile=mos2ex02_1s.str master
solve init 
method newton trap carr=1 electron
solve
solve vdrain=-0.1

log outf=mos2ex02_1s.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

### STRESSED TEST AT 1000 s
go atlas
# IMPORT MESH
mesh inf=mos2ex02_1000s.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
load infile=mos2ex02_1000s.str master
solve init 
method newton trap carr=1 electron
solve
solve vdrain=-0.1

log outf=mos2ex02_1000s.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

### STRESSED TEST AT 10000 s
go atlas
# IMPORT MESH
mesh inf=mos2ex02_10000s.str master.in

# MATERIAL CONTACT INTERFACE AND MODELS
contact name=gate p.poly
interf qf=5.e10
models temp=300 print srh auger cvt  

# SOLUTIONS
load infile=mos2ex02_10000s.str master
solve init 
method newton trap carr=1 electron
solve
solve vdrain=-0.1

log outf=mos2ex02_10000s.log
solve vgate=-0.1 vstep=-0.05 name=gate vfinal=-1.2

tonyplot -overlay mos2ex02_10000s.log mos2ex02_1000s.log mos2ex02_1s.log mos2ex02_0_1s.log mos2ex02_0_01s.log mos2ex02_2.log

quit
```

## 空穴捕获

```python
# 100nm 有效沟道长度 PMOS 建模
# pmos_ht: 空穴捕获

go atlas 

mesh inf=pmosvt.str master.in


material material=Si taun0=1e-7 taup0=1e-7
model temp=373 mos
contact name=gate workf=4.75

# refinement of the simulation grid
solve init
solve prev
solve vgate=-0.1
solve vgate=-0.5
solve vgate=-1.2
regrid potential ratio=0.5 max.level=2 smooth.k=4

save outf=mos2ex23_0.str



go atlas 

mesh infile=mos2ex23_0.str master.in


trap material=SiO2 subcontact=Silicon \
x.min=0.3 x.max=0.4 y.min=-0.002 y.max=-0.000 \
nmp4.sto nmp4.samples=10 \
density=1.0e+20 sign=1e-14 sigp=1.0e-14 \
nmp4.et1=4.2 nmp4.et1.sd=-0.4 nmp4.s12s=2.0 nmp4.s12s.sd=1.0 nmp4.r12s=1.0 nmp4.r12s.sd=0.3 \
nmp4.et2=6.0 nmp4.s1s2=3.0 nmp4.s1s2.sd=0.5 nmp4.r1s2=1.0 \
nmp4.nu=1e13 nmp4.epsT2s=0.6 nmp4.epsT2s.sd=0.2 nmp4.eps2s2=0.2 nmp4.eps2s2.sd=0.2 nmp4.eps1s1=3.0 \
nmp4.nit=1e11 nmp4.Ea=1.65 nmp4.Ea.sd=0.40 nmp4.Ed=0.15 nmp4.Ed.sd=0.0 nmp4.gamma=8e-8 nmp4.nup=1e12

material material=Si taun0=1e-7 taup0=1e-7
material material=SiO2 me.tunnel=0.3 mh.tunnel=0.3
model temp=373 mos
contact name=gate workfunction=4.75

# simulation of the pre-stress conditions
solve init
solve prev
solve vdrain=0.1

probe ft.msc msc.state=1 x=0.351 y=-0.00051
probe ft.msc msc.state=2 x=0.351 y=-0.00051
probe ft.msc msc.state=3 x=0.351 y=-0.00051
probe ft.msc msc.state=4 x=0.351 y=-0.00051
probe ft.msc msc.state=5 x=0.351 y=-0.00051

# save the trapped oxide charges
output devdeg
save outf=mos2ex23_1.str
tonyplot mos2ex23_1.str -set mos2ex23_1.set

# transient simulation of the stress phase
output u.trantrap traps
method tol.time=1.0e+2 quasi
log outf=mos2ex23_1.log
solve vgate=-2.0 vdrain=0.1 ramptime=1.0e-6 tstep=1.0e-8 tfinal=1.0e+0
log off

tonyplot mos2ex23_1.log -set mos2ex23_2.set

# save the trapped oxide charges
output devdeg
save outf=mos2ex23_2.str
tonyplot mos2ex23_2.str -set mos2ex23_3.set

# transient simulation of the relaxation phase
output u.trantrap traps
method tol.time=1.0e-2 quasi
log outf=mos2ex23_2.log
solve vgate=0.0 vdrain=0.1 ramptime=1.0e-6 tstep=1.0e-8 tfinal=1.0e+3
log off

tonyplot mos2ex23_2.log -set mos2ex23_4.set

# save the trapped oxide charges
output devdeg
save outf=mos2ex23_3.str
tonyplot mos2ex23_3.str -set mos2ex23_5.set


go atlas

mesh infile=mos2ex23_0.str master.in

material material=Si taun0=1e-7 taup0=1e-7
model temp=373 mos
contact name=gate workfunction=4.75


loop steps=3

assign name="i_str" print n.value=1 delta=1
assign name="i_log" print n.value=3 delta=1

solve init
solve prev

# loading the trapped oxide charges
load infile=mos2ex23_${i_str}.str master

solve vdrain=+0.005
solve vdrain=+0.005 vgate=+0.25
solve vdrain=+0.005 vgate=+0.5
solve vdrain=+0.005 vgate=+1.0
solve vdrain=+0.005 vgate=+1.5
solve vdrain=+0.005 vgate=+2.0
solve vdrain=+0.005 vgate=+2.5

# calculation of the Id(Vg) curves
log outf=mos2ex23_${i_log}.log
solve vgate=+3.0 vstep=-0.1 vfinal=-3.0 name=gate

# extraction of the Vth
extract name="Vth${i_str}" -(xintercept(maxslope(curve(abs(v."gate"),abs(i."drain"))))\
        -abs(ave(v."drain"))/2.0)
log off

l.end

tonyplot -overlay mos2ex23_3.log mos2ex23_4.log mos2ex23_5.log -set mos2ex23_6.set

quit

```